use StdIO : all;
use Array : all;

//[+] contains at least 1 Dim array
//[*] is at least a scalar
int[*] sumNth(int n, int[+] a)
{
    if (n > 0)
    {
        a = transposeNthToFront(n,a);
    }
    s = [shape(a)[[0]]];
    a = with{
        ([0] <= iv < s): a[iv];
    } : fold(+,0);

    return a;
}

int[+] transposeNthToFront(int n, int[+] a)
{
    if(n > 1)
    {
        a = {[k] -> transposeNthToFront((n-1),a[[k]])};
    }
    a = {[i, j] -> a [[j, i]]};
    return a;
}

int main()
{
    print(transposeNthToFront(1, [ [ 1, 2, 3 ], [ 4, 5, 6 ] ]));                       // [[1,4], [2,5], [3,6]]
    print(transposeNthToFront(1, [ [ [ 1, 2 ], [ 3, 4 ] ], [ [ 5, 6 ], [ 7, 8 ] ] ])); //[[[1,2],[5,6]],[[3,4],[7,8]]]
    print(transposeNthToFront(2, [ [ [ 1, 2 ], [ 3, 4 ] ], [ [ 5, 6 ], [ 7, 8 ] ] ])); // [[[1,3],[5,7]],[[2,4],[6,8]]]
    print(sumNth(  0, [ [[1],[2]], [[3],[4]], [[5],[6]] ])); //[[9],[12]]
    print(sumNth(  1, [ [[1],[2]], [[3],[4]], [[5],[6]] ])); //[ [3], [7], [11] ]
    print(sumNth(  2, [ [[1],[2]], [[3],[4]], [[5],[6]] ])); //[ [1,2], [3,4], [5,6] ]

    return 0;
}